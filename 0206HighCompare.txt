Sub CompareWithArrayStandard_Fix()

    Dim ws3 As Worksheet, ws4 As Worksheet
    Dim col3 As Long, col4 As Long
    Dim cell As Range
    Dim arr3 As Variant, arr4 As Variant
    Dim lastRow3 As Long, lastRow4 As Long
    Dim i As Long, j As Long
    Dim targetValue As String, compareValue As String
    Dim found As Boolean
    Dim delRange As Range
    
    Set ws3 = ThisWorkbook.Worksheets("工作表3")
    Set ws4 = ThisWorkbook.Worksheets("工作表4")
    
    Set cell = ws4.Range("A1:Z1").Find("學別4p", LookIn:=xlValues, LookAt:=xlWhole)
    If cell Is Nothing Then MsgBox "工作表4 Error": Exit Sub
    col4 = cell.Column
    
    Set cell = ws3.Range("A1:Z1").Find("學號3h", LookIn:=xlValues, LookAt:=xlWhole)
    If cell Is Nothing Then MsgBox "工作表3 Error": Exit Sub
    col3 = cell.Column

    lastRow4 = ws4.Cells(ws4.Rows.Count, col4).End(xlUp).Row
    lastRow3 = ws3.Cells(ws3.Rows.Count, col3).End(xlUp).Row
    
     
    arr4 = ws4.Range(ws4.Cells(1, col4), ws4.Cells(lastRow4, col4)).Value
    arr3 = ws3.Range(ws3.Cells(1, col3), ws3.Cells(lastRow3, col3)).Value

    Application.ScreenUpdating = False
    
    For i = 2 To lastRow3
        targetValue = Replace(Replace(Trim(CStr(arr3(i, 1))), vbCr, ""), vbLf, "")
        
        If targetValue <> "" Then
            found = False
            For j = 2 To UBound(arr4)
                compareValue = Replace(Replace(Trim(CStr(arr4(j, 1))), vbCr, ""), vbLf, "")
                If compareValue = targetValue Then
                    found = True
                    Exit For
                End If
            Next j
            
            If Not found Then
                If delRange Is Nothing Then
                    Set delRange = ws3.Rows(i)
                Else
                    Set delRange = Union(delRange, ws3.Rows(i))
                End If
            End If
        End If
    Next i

    If Not delRange Is Nothing Then delRange.Delete
    
    On Error Resume Next
    Dim remainRow As Long
    remainRow = ws3.Cells(ws3.Rows.Count, col3).End(xlUp).Row
    For Each cell In ws3.Range(ws3.Cells(2, col3), ws3.Cells(remainRow, col3))
        cell.Errors(xlNumberAsText).Ignore = True
    Next cell
    On Error GoTo 0
    
    Application.ScreenUpdating = True

End Sub
